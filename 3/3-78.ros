#!/bin/sh
#|-*- mode:lisp -*-|#
#| <Put a one-line description here>
exec ros -Q -- $0 "$@"
|#
(progn ;;init forms
  (ros:ensure-asdf)
  #+quicklisp (ql:quickload '(swank printv SB-SPROF) :silent t)
  )

(defpackage :ros.script.3-78.3714118325
  (:use :cl))
(in-package :ros.script.3-78.3714118325)

(defconstant unforced (gensym))
(defstruct delayobj forced closure)

(defmacro delay (expr)
  (let ((self (gensym)))
    `(let ((,self (make-delayobj :forced unforced)))
       (setf (delayobj-closure ,self)
             #'(lambda ()
                 (setf (delayobj-forced ,self) ,expr)))
       ,self)))

(defun force (x)
  (if (delayobj-p x)
      (if (eq (delayobj-forced x) unforced)
          (funcall (delayobj-closure x))
          (delayobj-forced x))
      x))

;; (defmacro delay (expr)
;;   `(lambda () ,expr))

;; (defun force (delayed-object)
;;   (funcall delayed-object))

(defmacro cons-stream (a b)
  `(cons ,a (delay ,b)))


(defun stream-car (stream)
  (car stream))

(defun stream-cdr (stream)
  (force (cdr stream)))

(defun stream-null? (stream)
  (null stream))
(defvar the-empty-stream '())



(defun stream-enumerate-interval (low high)
  (if (> low high)
      the-empty-stream
      (cons-stream
       low
       (stream-enumerate-interval (+ low 1) high))))


(defun stream-ref(s n)
  (if (= n 0)
      (stream-car s)
      (stream-ref (stream-cdr s) (- n 1))))

(defun stream-filter (pred stream)
  (cond ((stream-null? stream) the-empty-stream)
        ((funcall pred (stream-car stream))
         (cons-stream (stream-car stream)
                      (stream-filter pred
                                     (stream-cdr stream))))
        (t (stream-filter pred (stream-cdr stream)))))



(defun stream-for-each (proc s)
  (if (stream-null? s)
      'done
      (progn (funcall proc (stream-car s))
             (stream-for-each proc (stream-cdr s)))))

(defun display-line (x)
  (print x))
(defun display-stream (s)
  (stream-for-each #'display-line s))

(defun stream-map (proc &rest argstreams)
  (if (stream-null? (car argstreams))
      the-empty-stream
      (cons-stream
       (apply proc (mapcar #'stream-car argstreams))
       (apply #'stream-map
              (cons proc (mapcar #'stream-cdr argstreams))))))



(defun add-streams (s1 s2)
  (stream-map #'+ s1 s2))

(defun mul-streams(s1 s2)
  (stream-map #'* s1 s2))


(defvar *ones*)
(setq *ones* (cons-stream 1 *ones*))

(defvar *integers*)
(setq *integers*
      (cons-stream 1 (add-streams *ones* *integers*)))


(defun stream-take (s n)
  (if (= n 0)
      nil
      (cons (stream-car s) (stream-take (stream-cdr s) (- n 1)))))

(defun partial-sums (stream)
  (labels ((psum (s acc)
             (let ((tail (stream-cdr s))
                   (tmp (+ (stream-car s) acc)))
               (cons-stream tmp
                            (psum tail tmp)))))
    (psum stream 0)))

(defun scale-stream(stream factor)
  (stream-map (lambda (x) (* x factor)) stream))

(defun integrate-series (s)
  (mul-streams s
               (stream-map (lambda (x) (/ 1 x)) *integers*)))

(defvar *exp-series*)
(setq *exp-series* (cons-stream 1 (integrate-series *exp-series*)))

(defvar *cosine-series*)
(defvar *sine-series*)

(setq *cosine-series*
      (cons-stream
       1
       (stream-map #'- (integrate-series *sine-series*))))

(setq *sine-series*
      (cons-stream
       0
       (integrate-series *cosine-series*)))

;; (defun integral (delayed-integrand initial-value dt)
;;   (cons-stream initial-value
;;                (let ((integrand (force delayed-integrand)))
;;                  (if (stream-null? integrand)
;;                      the-empty-stream
;;                      (integral (delay (stream-cdr integrand))
;;                                (+ (* dt (stream-car integrand))
;;                                   initial-value)
;;                                dt)))))

(defun integral (delayed-integrand initial-value dt)
  (cons-stream
   initial-value
   (let ((integrand (force delayed-integrand)))
     (if (stream-null? integrand)
         the-empty-stream
         (integral
          (delay (stream-cdr integrand))
          (+  (* dt (stream-car integrand))
              initial-value)
          dt)))))

(defun solve-2nd (a b dt dy0 y0)
  (let ((y)
        (dy)
        (ddy))
    (setf y (integral (delay dy) y0 dt))
    (setf dy (integral (delay ddy) dy0 dt))
    (setf ddy (add-streams (scale-stream dy a)
                           (scale-stream y b)))

    y))


;; onlisp の 15.4 Lazy Evaluation
;; での force,delay 関数をつかうことで改善
(defun main (&rest argv)
  (declare (ignorable argv))
  (setq printv:*printv-output* *standard-output*)
  (printv:printv (stream-ref (solve-2nd 1 0 0.001 1 1) 1000)))
;;; vim: set ft=lisp lisp:
