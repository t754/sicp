#!/bin/sh
#|-*- mode:lisp -*-|#
#| <Put a one-line description here>
exec ros -Q -- $0 "$@"
|#
(progn ;;init forms
  (ros:ensure-asdf)
  #+quicklisp (ql:quickload '(swank printv) :silent t)
  )
(defpackage :ros.script.3-81.3714329317
  (:use :cl))
(in-package :ros.script.3-81.3714329317)

(defvar UNFORCED (gensym))
(defstruct delayobj forced closure)

(defmacro delay (expr)
  (let ((self (gensym)))
    `(let ((,self (make-delayobj :forced UNFORCED)))
       (setf (delayobj-closure ,self)
             #'(lambda ()
                 (setf (delayobj-forced ,self) ,expr)))
       ,self)))

(defun force (x)
  (if (delayobj-p x)
      (if (eq (delayobj-forced x) UNFORCED)
          (funcall (delayobj-closure x))
          (delayobj-forced x))
      x))

;; (defmacro delay (expr)
;;   `(lambda () ,expr))

;; (defun force (delayed-object)
;;   (funcall delayed-object))

(defmacro cons-stream (a b)
  `(cons ,a (delay ,b)))


(defun stream-car (stream)
  (car stream))

(defun stream-cdr (stream)
  (force (cdr stream)))

(defun stream-null? (stream)
  (null stream))
(defvar the-empty-stream '())



(defun stream-enumerate-interval (low high)
  (if (> low high)
      the-empty-stream
      (cons-stream
       low
       (stream-enumerate-interval (+ low 1) high))))


(defun stream-ref(s n)
  (if (= n 0)
      (stream-car s)
      (stream-ref (stream-cdr s) (- n 1))))

(defun stream-filter (pred stream)
  (cond ((stream-null? stream) the-empty-stream)
        ((funcall pred (stream-car stream))
         (cons-stream (stream-car stream)
                      (stream-filter pred
                                     (stream-cdr stream))))
        (t (stream-filter pred (stream-cdr stream)))))



(defun stream-for-each (proc s)
  (if (stream-null? s)
      'done
      (progn (funcall proc (stream-car s))
             (stream-for-each proc (stream-cdr s)))))

(defun display-line (x)
  (print x))
(defun display-stream (s)
  (stream-for-each #'display-line s))

(defun stream-map (proc &rest argstreams)
  (if (stream-null? (car argstreams))
      the-empty-stream
      (cons-stream
       (apply proc (mapcar #'stream-car argstreams))
       (apply #'stream-map
              (cons proc (mapcar #'stream-cdr argstreams))))))



(defun add-streams (s1 s2)
  (stream-map #'+ s1 s2))

(defun mul-streams(s1 s2)
  (stream-map #'* s1 s2))


(defvar *ones*)
(setq *ones* (cons-stream 1 *ones*))

(defvar *integers*)
(setq *integers*
      (cons-stream 1 (add-streams *ones* *integers*)))


(defun stream-take (s n)
  (if (= n 0)
      nil
      (cons (stream-car s) (stream-take (stream-cdr s) (- n 1)))))

(defun partial-sums (stream)
  (labels ((psum (s acc)
             (let ((tail (stream-cdr s))
                   (tmp (+ (stream-car s) acc)))
               (cons-stream tmp
                            (psum tail tmp)))))
    (psum stream 0)))

(defun scale-stream(stream factor)
  (stream-map (lambda (x) (* x factor)) stream))

(defun integrate-series (s)
  (mul-streams s
               (stream-map (lambda (x) (/ 1 x)) *integers*)))

(defvar *exp-series*)
(setq *exp-series* (cons-stream 1 (integrate-series *exp-series*)))

(defvar *cosine-series*)
(defvar *sine-series*)

(setq *cosine-series*
      (cons-stream
       1
       (stream-map #'- (integrate-series *sine-series*))))

(setq *sine-series*
      (cons-stream
       0
       (integrate-series *cosine-series*)))

(defvar *random-init* 123)
(defun rand-update (x)
  "custom xorshift 32"
  (setq x (logxor x (ash x 13)))
  (setq x (logxor x (ash x -17)))
  (setq x (logxor x (ash x 5)))
  (logand x (- (ash 1 32) 1)))


;; ('generate) or ('reset number)   stream
(defvar *command-stream*
  (stream-map (lambda (x) (if (zerop (mod x 5))
                              (list 'reset 888)
                              (list 'generate)))
              *integers*))

(defun make-random-stream (command-stream)
  (let ((tmp))
    (setf tmp (cons-stream *random-init*
                           (stream-map (lambda (rand-num command)
                                         (case (car command)
                                           (generate (rand-update rand-num))
                                           (reset (cadr command))
                                           (otherwise (error "Unknow command ~S" (car command)))))
                                       tmp
                                       command-stream)))
    tmp))

(defun main (&rest argv)
  (declare (ignorable argv))
  (setq printv:*printv-output* *standard-output*)
  (printv:printv
   (stream-take *command-stream* 20)
   (stream-take (make-random-stream *command-stream*) 20)))
;;; vim: set ft=lisp lisp:
